buildscript { repositories { jcenter() } }

plugins {
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'nebula.maven-publish' version '4.2.2'
    id 'nebula.javadoc-jar' version '4.2.2'
    id 'nebula.source-jar' version '4.2.2'
    id 'nebula.maven-apache-license' version '4.2.2'
    id 'nebula.nebula-release' version '3.0.5'
    id 'nebula.nebula-bintray' version '3.1.0'
    id 'nebula.dependency-lock' version '3.0.0'
    id 'nebula.info' version '3.0.3'
    id 'nebula.contacts' version '3.0.1'
    id 'com.github.kt3k.coveralls' version '2.4.0'
    id "org.ysb33r.gradletest" version "0.5.4"
}

description 'Test harness for Gradle plugins. Hopefully retiring in favor of Gradle TestKit'

group = 'com.netflix.nebula'

contacts {
    'justin@halfempty.org' {
        moniker 'Justin Ryan'
        github 'quidryan'
    }
    'rob.spieldenner@gmail.com' {
        moniker 'Rob Spieldenner'
        github 'rspieldenner'
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    compile ('org.spockframework:spock-core:1.0-groovy-2.4') { exclude group: 'org.codehaus.groovy' }
    // Waiting for this to be published to maven central
    // https://bintray.com/robfletcher/maven/spock-extensions/view
    // compile ('com.energizedwork:spock-extensions:1.0') { exclude group: 'org.codehaus.groovy' }
    compile 'cglib:cglib-nodep:2.2.2' // For Mocking classes with Spock
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

gradleTest {
    versions '2.2.1', '2.3', '2.4', '2.5', '2.6'
}

gradleTest.doFirst {
    project.version = project.version.toString()
}
gradleTest.dependsOn jar
